buildscript {
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
            jcenter()
        }

        dependencies {
            classpath 'com.android.tools.build:gradle:3.5.3'
        }
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def isNewArchitectureEnabled() {
        return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
    }

android {
    compileSdkVersion safeExtGet('ReactNativeUnity_compileSdkVersion', 31)
    defaultConfig {
        minSdkVersion safeExtGet('ReactNativeUnity_minSdkVersion', 21)
        targetSdkVersion safeExtGet('ReactNativeUnity_targetSdkVersion', 31)
        versionCode 1
        versionName "1.0"
        buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())


    }
    sourceSets {
        main {
            if (isNewArchitectureEnabled()) {
                java.srcDirs += ["src/fabric/java"]
            } else {
                java.srcDirs += ["src/paper/java"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"  // From node_modules
    implementation project(':unityLibrary')
    implementation files("${project(':unityLibrary').projectDir}/libs/unity-classes.jar")
}
